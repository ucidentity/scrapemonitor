group 'edu.berkeley.calnet'
version '0.1-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.7

mainClassName = "edu.berkeley.scrapemonitor.ScrapeMonitorDaemon"

repositories {
    mavenLocal()
    // If using a Maven proxy, put the property settings in
    // ~/.gradle/gradle.properties for default_proxy_url,
    // default_proxy_username and default_proxy_password.
    if (project.hasProperty("default_proxy_url")) {
        maven {
            url property("default_proxy_url")
            credentials {
                username property("default_proxy_username")
                password property("default_proxy_password")
            }
        }
    }
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    // Slf4j and Log4j2
    def slf4jVersion = '1.7.20'
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    def log4jVersion = '2.5'
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    //compile "org.apache.logging.log4j:log4j-to-slf4j:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"

    // Camel
    def camelVersion = '2.17.0'
    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-stream:$camelVersion"
    compile "org.apache.camel:camel-mail:$camelVersion"

    // GreenMail for testing SMTP
    testCompile "com.icegreen:greenmail:1.5.0"
    testCompile "com.icegreen:greenmail-standalone:1.5.0"

    compile "software.javatailer:javatailer:0.1.2"
}

test {
    maxParallelForks = 1
}

run {
    /**
     * Notable system properties that may be of interest:
     * edu.berkeley.scrapemonitor.config_file - ScrapeMonitor configuration file location.
     * log4j.configurationFile - Log4j2 configuration file location.
     */

    // Pass along all system properties for the running application.
    // This enables passing along "./gradlew -D..." parameters. 
    // Example: ./gradlew -Dlog4j.configurationFile=etc/log4j2.properties
    systemProperties System.getProperties()

    // If you need to, can add additional run-time classpath files or
    // directories with:
    // classpath += files(...)
}
